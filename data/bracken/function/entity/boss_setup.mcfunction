##########################################################
# Description: Check for specific boss before running boss tick function.
# Creators: Bracken and Sulfenir
##########################################################


#### Death Detection
execute if entity @s[type=marker,tag=bp.boss_death_detector] run return run function bracken:entity/boss/boss_death_detection


#### Bosses - ordered by most likely to least likely to encounter

#fae_emperor
execute if entity @s[type=minecraft:stray,tag=bp.fae_emperor] if entity @p[distance=..200] run return run function bracken:entity/boss/fae_emperor/tick

#unsung
execute if entity @s[type=minecraft:stray,tag=bp.unsung] if entity @p[distance=..200] run return run function bracken:entity/boss/unsung/tick
execute if entity @s[type=minecraft:marker,tag=bp.unsung] if entity @p[distance=..25] run return run function bracken:entity/boss/unsung/misc/closing_comment

#ostiarius
execute if entity @s[type=minecraft:marker,tag=bp.ostiarius] if entity @p[distance=..200] run return run function bracken:entity/boss/ostiarius/tick
execute if entity @s[type=minecraft:giant,tag=bp.ostiarius.core] if entity @p[distance=..200] run return run function bracken:entity/boss/ostiarius/core/tick

#the disruptor
execute if entity @s[type=minecraft:elder_guardian,tag=bp.the_disruptor] if entity @p[distance=..200] run return run function bracken:entity/boss/the_disruptor/tick

#bygone viceroy
execute if entity @s[type=minecraft:wither_skeleton,tag=bp.bygone_viceroy] if entity @p[distance=..200] run return run function bracken:entity/boss/bygone_viceroy/tick

#sporangium
execute if entity @s[type=minecraft:slime,tag=bp.sporangium] if entity @p[distance=..200] run return run function bracken:entity/boss/sporangium/tick

#kraken
execute if entity @s[type=minecraft:squid,tag=bp.kraken] if entity @p[distance=..200] run return run function bracken:entity/boss/kraken/tick

#solatium thrall
execute if entity @s[type=minecraft:stray,tag=bp.solatium_thrall] if entity @p[distance=..200] run return run function bracken:entity/boss/solatium_thrall/tick

#orbiter
execute if entity @s[type=ghast,tag=bp.orbiter] if entity @p[distance=..400] run return run function bracken:entity/boss/orbiter/functions